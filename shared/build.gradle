apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 29        // Not raised to 28 due to NotificationManagerExtender

    defaultConfig {
        minSdkVersion this.minSdkVersion
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            buildConfigField("boolean", "CRASHLYTICS_ENABLED", "true")
        }
        debug {
            ext.enableCrashlytics = false
            buildConfigField("boolean", "CRASHLYTICS_ENABLED", "${ext.enableCrashlytics}")
        }
    }

    kotlinOptions.jvmTarget = "1.8"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        check 'NewApi'
        abortOnError true
        xmlReport false
        textReport true
        textOutput "stdout"
    }

    // Enable lint checking in all build variants.
    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def lintTask = tasks["lint${variant.name.capitalize()}"]
            output.assemble.dependsOn lintTask
        }
    }
}

dependencies {
    api project(':deagle')
    api 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.jetbrains:annotations:15.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlin_coroutine_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'com.google.android.gms:play-services-analytics:17.0.0'
    implementation 'com.google.firebase:firebase-core:17.4.4'
    implementation 'com.google.firebase:firebase-analytics:17.4.4'
    implementation 'com.google.firebase:firebase-config:19.2.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.oasisfeng.condom:library:2.5.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.10.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.18.3'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlin_coroutine_version"
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
}
