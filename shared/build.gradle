apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 29        // Not raised to 28 due to NotificationManagerExtender

    defaultConfig {
        minSdkVersion 21
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            buildConfigField("boolean", "CRASHLYTICS_ENABLED", "false")
        }
        debug {
            ext.enableCrashlytics = false
            buildConfigField("boolean", "CRASHLYTICS_ENABLED", "${ext.enableCrashlytics}")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        check 'NewApi'
        abortOnError true
        xmlReport false
        textReport true
        textOutput "stdout"
    }

    // Enable lint checking in all build variants.
    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def lintTask = tasks["lint${variant.name.capitalize()}"]
            output.assemble.dependsOn lintTask
        }
    }
}

dependencies {
    api project(':deagle')
    api 'com.google.code.findbugs:jsr305:3.0.2'
    api 'net.sourceforge.streamsupport:android-retrostreams:1.7.1'
    api 'net.sourceforge.streamsupport:android-retrofuture:1.7.1'
    implementation project(':legacy')
    implementation 'org.jetbrains:annotations:15.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.oasisfeng.condom:library:2.5.0'
    implementation "androidx.core:core:$core_version"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.18.3'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
}
